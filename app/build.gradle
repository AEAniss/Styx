apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId 'com.jamal2367.styx'
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 120
        versionName '12.0'
        resConfigs "ar", "cs", "de", "el", "en", "es", "fr", "hu", "it", "iw", "ja", "ko", "lt", "nl", "no", "pl", "pt", "pt-rBR", "ro", "ru", "sr", "sv", "tr", "uk", "vi", "zh-rCN", "zh-rTW"
        setProperty("archivesBaseName", 'Styx_' + versionName)
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin //
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    // Google Stuff //
    implementation 'com.google.android.material:material:1.4.0-beta01'
    implementation 'com.google.code.gson:gson:2.8.6'

    // AndroidX Stuff //
    implementation 'androidx.fragment:fragment-ktx:1.3.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.webkit:webkit:1.4.0'

    // Dependencies Injection //
    implementation 'com.google.dagger:dagger:2.35.1'
    kapt 'com.google.dagger:dagger-compiler:2.35.1'

    // Permissions //
    implementation 'com.anthonycr.grant:permissions:1.1.2'

    // OkHttp3 //
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'

    // RxJava //
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    // Reading mode //
    implementation 'net.dankito.readability4j:readability4j:1.0.6'

    // File Reading //
    implementation 'com.anthonycr.mezzanine:mezzanine:1.1.1'
    kapt 'com.anthonycr.mezzanine:mezzanine-compiler:1.1.1'

    // Volley HTTP //
    implementation 'com.android.volley:volley:1.2.0'

    // Code Editor //
    implementation 'com.github.jamal2362:CodeEditor:3.0.0'
}

kapt {
    arguments {
        arg("mezzanine.projectPath", project.rootDir)
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'

    }
}

def getEnabledLocales() {
    def resDir = file('src/main/res')

    def potentialLanguageDirs = resDir.listFiles(new FilenameFilter() {
        @Override
        boolean accept(File dir, String name) {
            return name.startsWith("values-")
        }
    })

    def langs = potentialLanguageDirs.findAll {
        // Only select locales where strings.xml exists
        // Some locales might only contain e.g. sumo URLS in urls.xml, and should be skipped (see es vs es-ES/es-MX/etc)
        return  file(new File(it, "strings.xml")).exists()
    } .collect {
        // And reduce down to actual values-* names
        return it.name
    } .collect {
        return it.substring("values-".length())
    } .collect {
        if (it.length() > 3 && it.contains("-r")) {
            // Android resource dirs add an "r" prefix to the region - we need to strip that for java usage
            // Add 1 to have the index of the r, without the dash
            def regionPrefixPosition = it.indexOf("-r") + 1

            return it.substring(0, regionPrefixPosition) + it.substring(regionPrefixPosition + 1)
        } else {
            return it
        }
    }.collect {
        return '"' + it + '"'
    }

    // en-US is the default language (in "values") and therefore needs to be added separately
    langs << "\"en-US\""

    return langs.sort { it }
}
